dnl -*- autoconf -*-

dnl Initialization
dnl ==============

dnl FB_INIT
dnl -------
dnl Initialize Autoconf and self
m4_define([FB_INIT], [
  AC_PREREQ([2.69])
  AC_INIT([foundation], [m4_chomp(m4_esyscmd([git describe]))])
  AC_REVISION([m4_chomp(m4_esyscmd([git describe --long]))])
  _FB_INIT_HELP
  _FB_INIT_BOOTSTRAP

  FB_ARG_VAR( [FOUNDATION_ROOT], [Root directory of the Foundation repository],
              [`(cd "$srcdir/../.." ; pwd)`] )
])dnl FB_INIT

dnl _FB_INIT_HELP
dnl -------------
dnl Override help header to avoid cluttering `./configure --help' with
dnl irrelevant software build options
m4_define([_FB_INIT_HELP],
 [ AC_PRESERVE_HELP_ORDER
   m4_cleardivert([HELP_BEGIN])
   m4_divert_push([HELP_BEGIN])
if test "$ac_init_help" = "long"; then
    cat <<_ACEOF
\`$[0]' configures m4_ifset([AC_PACKAGE_STRING],
                            [AC_PACKAGE_STRING],
                            [this package]) to adapt to many kinds of systems.

Usage: $[0] [[OPTION]]... [[VAR=VALUE]]...

[To assign environment variables (e.g., NAME, FQDN...), specify them as
VAR=VALUE.  See below for descriptions of some of the useful variables.

Defaults for the options are specified in brackets.

Configuration:
  -h, --help              display this help and exit
      --help=short        display options specific to this package
  -V, --version           display version information and exit
  -q, --quiet, --silent   do not print \`checking ...' messages
      --cache-file=FILE   cache test results in FILE [disabled]
  -C, --config-cache      alias for \`--cache-file=config.cache'
  -n, --no-create         do not create output files
      --srcdir=DIR        find the sources in DIR [configure dir or \`..']
]
m4_divert_pop([HELP_BEGIN])])dnl _FB_INIT_HELP

dnl _FB_INIT_BOOTSTRAP
dnl ------------------
dnl Initializes bootstrap.sh.in and sets up hooks to embed it in config.status.
m4_define([_FB_INIT_BOOTSTRAP],
  [ cat > ./bootstrap.sh.in.tmp <<_FB_EOF
#!/bin/sh
set -e -x
_FB_EOF

    AC_CONFIG_COMMANDS_PRE([
      AC_CONFIG_FILES( [bootstrap.sh],
                       [chmod 0755 bootstrap.sh ; rm $srcdir/bootstrap.sh.in],
                       [cat >$srcdir/bootstrap.sh.in <<_FB_BOOTSTRAP_SH_IN_EOF
`cat ./bootstrap.sh.in.tmp`
_FB_BOOTSTRAP_SH_IN_EOF
rm ./bootstrap.sh.in.tmp])])
])dnl _FB_INIT_BOOTSTRAP

dnl Definitions
dnl ===========

dnl FB_RUN(COMMAND)
dnl ---------------
dnl Add COMMAND to bootstrap.sh.in.
dnl
dnl AC_CONFIG_FILES is used later to generate bootstrap.sh, so COMMAND
dnl may include @SUBSTITUTIONS@.
AC_DEFUN([FB_RUN], [
AS_ECHO(["AS_ESCAPE([$1], [\"])"]) >> ./bootstrap.sh.in.tmp
])dnl FB_RUN

dnl FB_ARG_VAR(VARNAME, DOCUMENTATION, DEFAULT)
dnl -------------------------------------------
dnl Same as AC_ARG_VAR, but makes it possible to specify default value
AC_DEFUN( [FB_ARG_VAR],
  [ AC_ARG_VAR([$1], [$2])
    m4_ifnblank([$3],
      [ AS_VAR_SET_IF( [$1], [],
          [AC_SUBST([$1], [$3])])])
])dnl FB_ARG_VAR

dnl FB_TRY_PROG(PROG-TO-CHECK-FOR, ACTION-IF-NOT-FOUND)
dnl ---------------------------------------------------
dnl Calls AC_CHECK_PROG and runs ACTION-IF-NOT-FOUND if PROG-TO-CHECK-FOR
dnl was not found. ACTION-IF-NOT-FOUND is most likely FB_RUN with whatever
dnl command will install PROG-TO-CHECK-FOR.
AC_DEFUN([FB_TRY_PROG],[
  AC_CHECK_PROG([_fb_has_prog_]AS_TR_SH($1), [$1], [yes])
  AS_IF([test "x$_fb_has_prog_]AS_TR_SH($1)[" != "xyes"], [$2])])

dnl FB_INSTALL(PACKAGE_NAME)
dnl ------------------------
dnl Install package from Apt in the bootstrap script. Before first
dnl `apt-get' invocation, DEBIAN_FRONTEND is set to `noninteractive',
dnl and `apt-get update' is called.
AC_DEFUN([FB_INSTALL],
  [ AS_IF( [test "x$_fb_install_apt_configured" = "x"],
           [ FB_RUN([export DEBIAN_FRONTEND=noninteractive])
             FB_RUN([apt-get update --yes]) ])
    _fb_install_apt_configured=yes
    FB_RUN([apt-get install --yes $1])
])dnl FB_INSTALL

dnl FB_CONFIG_FILE(FILENAME, [TEMPLATE-CONTENTS])
dnl ---------------------------------------------
dnl Shortcut to AC_CONFIG_FILES to make self-contained configure script.
dnl 
dnl It is the same as AC_CONFIG_FILES, but input file is generated by
dnl configure.status in runtime, and deleted immediately after rendering.
dnl Contents of the input file can be provided as second argument; if they
dnl are not provided, `autoconf' will insert contents of `FILENAME.in.src'
dnl into the configure script.
AC_DEFUN([FB_CONFIG_FILE],
  [ AC_CONFIG_FILES([$1], [rm $srcdir/$1.in], [
cat > $srcdir/$1.in <<_FB_CONFIG_FILE_EOF
AS_ESCAPE(m4_default([$2], [m4_builtin([undivert], [$1.in.src])]))
_FB_CONFIG_FILE_EOF])
])dnl FB_CONFIG_FILE
